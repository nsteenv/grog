apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "eclipse"
apply plugin: "jetty"

group = "fr.vansteen.grog"
version = "0.1.0-SNAPSHOT"

description = "Generate static website from asciidoc files and groovy templates"

repositories {
    jcenter()
    maven {
        // maven repo for spock snapshot
        url "http://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.asciidoctor:asciidoctor-java-integration:0.1.4"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.0-SNAPSHOT"
}

task grogClean(dependsOn: clean, type: Delete) {
    description = "Clean all generated static webpages"
    delete "$project.buildDir/grog"
}

task grogPublish(type: GradleBuild) {
    description = "Publish static webpages on Github"
    buildFile = "publish.gradle"
    tasks = ["publishGhPages"]
}

task grogCopyAssets(dependsOn: grogClean, type: Copy) {
    description = "Copy assets to build directory"
    from "${project.projectDir}/src/grog/assets"
    into "${project.buildDir}/grog"
    includeEmptyDirs = false
}

task grogGenerate(dependsOn: grogCopyAssets, type: GradleBuild) {
    description = "Generate static webpages"
    buildFile = "asciidoc.gradle"
    tasks = ["generateTemplates"]
}

task grogPreview(type: GradleBuild) {
    description = "Preview generated pages with jetty"
    buildFile = "jetty.gradle"
    tasks = ["startServer"]
}

idea {
    module {
        excludeDirs = [file(".gradle")]
        ["classes", "docs", "dependency-cache", "libs", "reports", "resources", "test-results", "tmp"].each {
            excludeDirs << file("$buildDir/$it")
        }
    }
}


